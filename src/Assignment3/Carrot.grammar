/**********************************************************************
 Grammar for Carrot programs
 **********************************************************************/
program         ::= declList /* DONE*/
                ;

declList        ::= declList decl /* DONE*/
                | /* epsilon */ /* DONE*/
                ;

decl            ::= varDecl /* DONE*/
                | fnDecl /* DONE*/
                | structDecl /* DONE*/      // struct definitions only at top level
                ;

varDeclList     ::= varDeclList varDecl /* DONE*/
                | /* epsilon */ /* DONE*/
                ;

varDecl         ::= type id SEMICOLON  /* DONE*/
                | STRUCT id id SEMICOLON
                ;

fnDecl          ::= type id formals fnBody  /* DONE*/
                ;
                
structDecl      ::= STRUCT id LCURLY structBody RCURLY SEMICOLON /* DONE*/
                ;
                
structBody      ::= structBody varDecl /* DONE*/
                | varDecl /* DONE*/
                ;
                
formals         ::= LPAREN RPAREN /* DONE*/
                | LPAREN formalsList RPAREN /* DONE*/
                ;

formalsList     ::= formalDecl /* DONE*/
                | formalDecl COMMA formalsList /* DONE*/
                ;

formalDecl      ::= type id  /* DONE*/       // note: no struct parameters
                ;

fnBody          ::= LCURLY varDeclList stmtList RCURLY  /* DONE*/
                ;

stmtList        ::= stmtList stmt /* DONE*/
                | /* epsilon */ /* DONE*/
                ;

stmt            ::= assignExp SEMICOLON /* DONE*/
                | loc PLUSPLUS SEMICOLON /* DONE*/
                | loc MINUSMINUS SEMICOLON /* DONE*/
                | CIN READ loc SEMICOLON /* DONE*/
                | COUT WRITE exp SEMICOLON /* DONE*/
                | IF LPAREN exp RPAREN LCURLY varDeclList stmtList RCURLY /* DONE*/
                | IF LPAREN exp RPAREN LCURLY varDeclList stmtList RCURLY ELSE LCURLY varDeclList stmtList RCURLY /* DONE*/
                | WHILE LPAREN exp RPAREN LCURLY varDeclList stmtList RCURLY /* DONE*/
		        | REPEAT LPAREN exp RPAREN LCURLY varDeclList stmtList RCURLY /* DONE*/
                | RETURN exp SEMICOLON /* DONE*/
                | RETURN SEMICOLON /* DONE*/
                | fncall SEMICOLON /* DONE*/
                ;

assignExp       ::= loc ASSIGN exp /* DONE*/
                ;
                
exp             ::= assignExp /* DONE*/
                | exp PLUS exp /* DONE*/
                | exp MINUS exp /* DONE*/
                | exp TIMES exp /* DONE*/
                | exp DIVIDE exp /* DONE*/
                | NOT exp /* DONE*/
                | exp AND exp /* DONE*/
                | exp OR exp /* DONE*/
                | exp EQUALS exp /* DONE*/
                | exp NOTEQUALS exp /* DONE*/
                | exp LESS exp /* DONE*/
                | exp GREATER exp /* DONE*/
                | exp LESSEQ exp /* DONE*/
                | exp GREATEREQ exp /* DONE*/
                | MINUS term /* DONE*/
                | term /* DONE*/
                ;

term            ::= loc /* DONE*/
                | INTLITERAL /* DONE*/
                | STRINGLITERAL /* DONE*/
                | TRUE /* DONE*/
                | FALSE /* DONE*/
                | LPAREN exp RPAREN /* DONE*/
                | fncall /* DONE*/
                ;

fncall          ::=  id LPAREN RPAREN   // fn call with no args  /* DONE*/
                | id LPAREN actualList RPAREN  // with args  /* DONE*/
                ;

actualList      ::= exp /* DONE*/
                | actualList COMMA exp /* DONE*/
                ;

type            ::= INT /* DONE*/
                | BOOL /* DONE*/
                | VOID /* DONE*/
                ;

loc             ::= id /* DONE*/
                | loc DOT id /* DONE*/

id              ::= ID /* DONE*/
                ;

